################################################################################
#
# RevBayes Validation Test: Normal Distribution
#
# Model: 10 random variable from an normal distribution with
#        a uniform prior on the mean and exponential prior on
#        the standard deviation.
#
# Inference: MCMC algorithm with the following moves:
#           - mvSlide
#           - mvScale
#
# Authors: Sebastian Hoehna
#
################################################################################

n_samples = 10

mu ~ dnUniform( -10, 10 )
sigma ~ dnExponential( 1.0 )

moves = VectorMoves()
moves.append( mvSlide(mu) )
moves.append( mvScale(sigma) )

for (i in 1:n_samples ) {
   x[i] ~ dnNormal(mu,sigma)
   x[i].clamp( 100.0 )
}

mymodel = model(mu)


# We do not need any monitors, so we create only the empty vector.
monitors = VectorMonitors()

mymcmc = mcmc(mymodel, monitors, moves)

validation = validationAnalysis(mymcmc, 2000)
validation.burnin(generations=1000,tuningInterval=100)
validation.run(generations=3000)
validation.summarize( coverageProbability=0.9 )


# you may want to quit RevBayes now
q()
